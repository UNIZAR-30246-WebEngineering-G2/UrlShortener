buildscript {
    repositories {
        mavenCentral()
        maven { url "http://repo.spring.io/release" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.4.3.RELEASE")
    }
}
apply plugin: "spring-boot"
apply plugin: 'eclipse-wtp'

jar {
    baseName = 'ingweb'
}

// Common project specific stuff
eclipse {
    project {
        name = 'UrlShortener.team'
    }
}


sourceSets.main.java.srcDirs += "${buildDir}/generated-sources/jaxb"

task genJaxb {
    ext.sourcesDir = "${buildDir}/generated-sources/jaxb"
    ext.schema = "src/main/resources/register.xsd"

    inputs.file ext.schema
    outputs.dir ext.sourcesDir

    doFirst() {
        project.ant {
            taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask",
                    classpath: configurations.jaxb.asPath
            mkdir(dir: sourcesDir)

            xjc(destdir: sourcesDir, schema: schema, encoding: 'UTF-8') {
                arg(value: "-wsdl")
                produces(dir: sourcesDir, includes: "**/*.java")
            }
        }

    }
}


task afterEclipseImport{
    dependsOn "genJaxb"
}

compileJava.dependsOn genJaxb

configurations {
    jaxb
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile project(':common')
    compile("org.thymeleaf:thymeleaf-spring4")

    //webSockets
    compile("org.springframework.boot:spring-boot-starter-websocket")
    compile("org.webjars:webjars-locator")
    compile("org.webjars:sockjs-client:1.0.2")
    compile("org.webjars:stomp-websocket:2.3.3")

    // Provides the production database
    runtime 'org.hsqldb:hsqldb'

    // Provides java script libraries for static content
    compile 'org.webjars:bootstrap:3.3.5'
    compile 'org.webjars:jquery:2.1.4'

    //soap
    compile("org.springframework.boot:spring-boot-starter-ws")
    compile "org.apache.httpcomponents:httpclient:4.5.2"
    compile "wsdl4j:wsdl4j:1.6.3"
    jaxb 	"com.sun.xml.bind:jaxb-core:2.2.11"
    jaxb 	"com.sun.xml.bind:jaxb-xjc:2.2.11"

    // Testing framework
    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.apache.httpcomponents:httpclient:4.5.2'
    testCompile 'com.jayway.jsonpath:json-path:2.2.0'
}

task wrapper(type: Wrapper){
    gradleVersion = '2.7'
}